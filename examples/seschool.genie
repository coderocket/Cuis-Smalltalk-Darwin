
Parameters 
 
epochs: 			15
generations: 35 

population: 			3200
instances: 			145

mutationp :			1.0e-3
inversionp :			2.0e-1

Schema 
 
day: 			1 .. 6
hour	:		hour(day)
lecturer:			lecturer(module(id))
 
Functions

hour

# day           hour

1                  8 .. 20
2                  8 .. 20
3                  8 .. 20
4                  8 .. 20
5                  8 .. 20
6                  8 .. 13

load seplan.genief

Keys

module : 		module(id) 
duration: duration(module(id))

Rules

bonus 10 ;

# a lecturer must not teach on days and hours that they have cordoned  

penalty for interval in timetable(lecturer, day) intersect(interval, hour .. hour + duration) ;
 
# the same lecturer cannot teach on the same day and hour more than one class
 
penalty group by (day, lecturer) overlap(hour .. hour + duration);

# if a lecturer teaches in a particular day then they teach at least 3 hours

penalty group by (day,lecturer) ( sum(duration) > 0) * (  (3 - sum(duration)) * sum(day < 6)) max 0 ;
 
# the students in a frame cannot study more than one class in the same hour.
 
penalty on morning group by (day) overlap(hour .. hour + duration);

penalty on evening group by (day) overlap(hour .. hour + duration);

# a lecturer cannot teach more than 20 hours in a week

penalty group by (lecturer) sum(duration) -  20 max 0;

# a lecturer must no teach more than 6 hours a day

penalty group by (day,lecturer) sum(duration) -  6 max 0;


# each frame must have at least one free day

penalty on morning count(day) - 4 max 0;

penalty on evening count(day) - 4 max 0;

# day tracks may not start lectures after 17

penalty on morning sum(hour - 17 max 0) ;

# evening tracks may not start lectures before 17 except on fridays
 
penalty on evening sum( (day < 6)*(17 - hour max 0)); 
penalty on evening sum( (day = 6)*(hour + duration - 15 max 0));

# gap between classes should not be more than 3 hours

penalty on morning group by (day) maxgap(hour .. hour + duration) - 3 max 0;

penalty on evening group by (day) maxgap(hour .. hour + duration) - 3 max 0;

# non empty morning frames have at least 3 hours

penalty on morning group by (day) (sum(duration) > 0) * (  3 - sum(duration)) max 0 ;

# morning frames do not study on friday

penalty on morning sum( day = 6 ) ;
